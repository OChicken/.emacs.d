# -*- mode: snippet -*-
# name: gnuplot-multiplot
# key:  gp-multiplot
# --
#+begin_src gnuplot :file /tmp/gp-multiplot.png :exports both
reset
set term png

# Functions (1/0 means not defined)
a = 0.9
f(x) = abs(x)<2*pi ? a*sin(x)           : 1/0
g(x) = abs(x)<2*pi ? a*sin(x+pi/2)      : 1/0
h(x) = abs(x)<2*pi ? a*sin(x+pi)        : 1/0
k(x) = abs(x)<2*pi ? a*sin(x+3.0/2*pi)  : 1/0

set tics scale 0.5
set ytics 1
set xrange [-2.5*pi:2.5*pi]
set yrange [-1.5:1.5]
set zeroaxis
set grid

# x- and ytics for each row resp. column
XTICS = "set xtics ('-2π' -2*pi, '-π' -pi,'0' 0, 'π' pi, '2π' 2*pi);\
          set xlabel 'x'"
YTICS = "set format y '%.2f'; set ylabel 'y'"
NOXTICS = "set format x ''; unset xlabel"
NOYTICS = "set format y ''; unset ylabel"
# Margins for each row resp. column
TMARGIN = "set tmargin at screen 0.90; set bmargin at screen 0.50"
BMARGIN = "set tmargin at screen 0.50; set bmargin at screen 0.10"
LMARGIN = "set lmargin at screen 0.15; set rmargin at screen 0.55"
RMARGIN = "set lmargin at screen 0.55; set rmargin at screen 0.95"

### Start multiplot (2x2 layout)
set multiplot layout 2,2 rowsfirst title "Multiplot 2x2"
# --- GRAPH a
@TMARGIN; @LMARGIN
@NOXTICS; @YTICS
plot f(x)
# --- GRAPH b
@TMARGIN; @RMARGIN
@NOXTICS; @NOYTICS
plot g(x)
# --- GRAPH c
@BMARGIN; @LMARGIN
@XTICS; @YTICS
plot h(x)
# --- GRAPH d
@BMARGIN; @RMARGIN
@XTICS; @NOYTICS
plot k(x)

unset multiplot
#+end_src
